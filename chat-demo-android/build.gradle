apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def ACCESS_TOKEN_SERVICE_URL = project.hasProperty('ACCESS_TOKEN_SERVICE_URL') ? "\""+project.getProperty('ACCESS_TOKEN_SERVICE_URL')+"\"" : "\"http://example.com/token\""

ext.appcompat_version = "28.0.0"
ext.play_services_version = "15.0.1"

repositories {
    google()
    maven { // For kotterknife
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$appcompat_version"
    implementation "com.android.support:design:$appcompat_version"

    // FCM
    // Firebase now requires the app gradle file to explicitly list
    // `com.google.firebase:firebase-core` as a dependency for Firebase services to work as expected.
    implementation 'com.google.firebase:firebase-core:16.0.6'
    implementation 'com.google.firebase:firebase-iid:17.0.4'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    // implementation "androidx.arch.core:core-runtime:1.1.1" // see https://stackoverflow.com/a/50976776

    // implementation "com.google.android.gms:play-services-base:$play_services_version"

    implementation "com.jakewharton:kotterknife:0.1.0-SNAPSHOT"
    implementation "eu.inloop:simplerecycleradapter:0.3.4"
    // implementation "com.android.support.constraint:constraint-layout:1.0.2"

    implementation project(":sdk")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.27.0-eap13'
    implementation "org.jetbrains.anko:anko:$anko_version"
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.twilio.chat.demo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 26
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ACCESS_TOKEN_SERVICE_URL", ACCESS_TOKEN_SERVICE_URL
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "ACCESS_TOKEN_SERVICE_URL", ACCESS_TOKEN_SERVICE_URL
        }
        coordinated.initWith(debug)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

     dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    sourceSets.main {
        jniLibs.srcDirs = [project(':sdk').buildDir.absolutePath + '/intermediates/binaries/debug/lib/']
    }

    /// Split APKs by ABI:
    // splits {
    //     abi {
    //         enable true
    //         universalApk true
    //     }
    // }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

// Keep an eye on SDK deprecations
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xdiags:verbose"
}

project.afterEvaluate {
    if (project.hasProperty("googleServicesJson")) {
        preBuild.doFirst {
            copy {
                from(googleServicesJson)
                into(projectDir)
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
